class_name Ball
extends Node2D

signal boosted(ball: Ball, movement: Movement)

@export var seek_steering: SeekSteering
@export var hit_stop_profile: HitStopProfile

@onready var hitbox: Hitbox = %Hitbox
@onready var movement: Movement = %Movement
@onready var hit_stop: HitStop = %HitStop
@onready var contact_damage: ContactDamage = %ContactDamage
@onready var boost: BallBoost = %BallBoost


func _ready() -> void:
	assert(seek_steering != null, "seek_steering must not be null.")
	assert(hit_stop_profile != null, "hit_stop_profile must not be null.")

	movement.setup(self)
	boost.setup(movement)

	hitbox.hit_detected.connect(_on_hit_detected)
	boost.boost_used.connect(_on_boost_used)
	boost.boost_used.connect(hit_stop.cancel_deferred)


func _physics_process(delta: float) -> void:
	if hit_stop.is_active():
		_use_boost()
		return

	var target_position := get_global_mouse_position()

	_update_velocity(target_position, delta)
	_use_boost()
	movement.move(delta)


func _update_velocity(target_position: Vector2, delta: float) -> void:
	var new_velocity := seek_steering.calculate_velocity(
		movement.get_velocity(),
		global_position,
		target_position,
		delta
	)

	movement.set_velocity(new_velocity)


func _use_boost() -> void:
	if Input.is_action_just_pressed("primary_action"):
		boost.use()


func _on_boost_used() -> void:
	boosted.emit(self, movement)


func _on_hit_detected(hurtbox: Hurtbox) -> void:
	if hurtbox == null:
		return

	var speed := movement.get_speed()
	var damage_amount := contact_damage.calculate_damage(speed)
	var hit_stop_duration := hit_stop_profile.get_duration(speed)

	hurtbox.receive_hit(damage_amount, speed)
	hit_stop.start(hit_stop_duration)
